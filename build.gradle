plugins {
    id 'fabric-loom' version '1.3.8'
    id 'maven-publish'
}

base.archivesName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = 'FabricMC Maven'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        name = 'TerraformersMC Maven'
        url = 'https://maven.terraformersmc.com/'
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        'still-clouds' {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    accessWidenerPath = file('src/main/resources/still-clouds.accesswidener')
}

/*
* To change dependency versions, see `./gradle.properties`.
*/
dependencies {
    // Loom Required Dependencies
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Project Dependencies
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property 'version', project.mod_version

    filesMatching('fabric.mod.json') {
        expand 'version': project.mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain.languageVersion = JavaLanguageVersion.of 17
}

jar {
    from('LICENSE') {
        rename { "${it}_${base.archivesName}"}
    }
}

testing {
    suites {
        test {
            useJUnitJupiter project.junit_jupiter_version

            dependencies {
                compileOnly sourceSets.client.compileClasspath
                implementation sourceSets.client.output
                runtimeOnly sourceSets.client.runtimeClasspath
            }
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
